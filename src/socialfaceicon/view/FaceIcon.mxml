<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml"
	 horizontalAlign="center"
	 backgroundAlpha="0"
	 backgroundColor="#999999"
	 verticalGap="2"
	 xmlns:view="socialfaceicon.view.*"
	 
	 mouseDown="onMouseDown(event)"
	 click="onClick(event)"
	 rollOver="onRollOver(event)"
	 rollOut="onRollOut(event)"
	 doubleClick="onDoubleClick()"
	 doubleClickEnabled="true"
	 >
	<mx:Script>
		<![CDATA[
			import flash.utils.clearTimeout;
			import flash.utils.setTimeout;
			import socialfaceicon.events.MouseDragStartEvent;
			import flash.net.navigateToURL;
			import mx.events.FlexEvent;
			import socialfaceicon.model.ImageCache;
			import mx.events.DragEvent;
			import mx.core.DragSource;
			import mx.managers.DragManager;
			import socialfaceicon.model.IUser;
			
			[Bindable]
			private var _iconName:String;
			private var _iconNameChanged:Boolean = false;
			private var _iconImageUrl:String;
			private var _iconImageUrlChanged:Boolean = false;
			private var _iconStatus:String;
			private var _iconStatusChanged:Boolean = false;
			private var imgCache:ImageCache;
			private var iconUserUrl:String;
			private var mousePauseTimeoutId:int;
			private var mouseDragStartEvent:MouseDragStartEvent;
			
			private static var selectedFaceIcon:FaceIcon = null;
			
			public function init(user:IUser):FaceIcon
			{
				if (user.iconName) {
					this._iconName = user.iconName;
					this._iconNameChanged = true;
				}
				if (user.iconImageUrl) {
					this._iconImageUrl = user.iconImageUrl;
					this._iconImageUrlChanged = true;
				}
				this.status = user.getIconCurrentStatus();
				this.iconUserUrl = user.iconUserUrl;
				return this;
			}
			
			protected override function commitProperties():void
			{
				super.commitProperties();
				
				if (this._iconNameChanged) {
					this._iconNameChanged = false;
					this.nameText.text = this._iconName;
				}
				if (this._iconStatusChanged) {
					this._iconStatusChanged = false;
					this.statusText.text = this._iconStatus;
				}
				if (this._iconImageUrlChanged) {
					this._iconImageUrlChanged = false;
					loadImage(this._iconImageUrl);
				}
			}
			
			public function set status(str:String):void {
				this._iconStatus = str;
				this._iconStatusChanged = true;
				this.invalidateProperties();
				this.invalidateDisplayList();
			}
			
			private function loadImage(url:String):void {
				if (imgCache) return;
				imgCache = new ImageCache();
				imgCache.addEventListener(
					Event.COMPLETE,
					onImageLoad);
				imgCache.addEventListener(
					IOErrorEvent.IO_ERROR,
					onImageIOError);
				imgCache.loadImage(url);
			}
			
			private function onImageLoad(event:Event):void {
				image.source = imgCache.bitmap;
				image.filters = [new DropShadowFilter(
					1,  // distance
					60, // angle
					0x000000,
					1,  // alpha
					3,  // blurX
					3,  // blurY
					2,  // strength
					2   // quality
				)]
				imgCache = null;
			}
			
			private function onImageIOError(event:IOErrorEvent):void {
				trace(([
					this.className,
					event.type
				]).join(": "));
				imgCache = null;
			}
			
			//
			// Mouse Drag Start
			//
			
			
			private function onMouseDown(mouse:MouseEvent):void {
				addEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
				addEventListener(MouseEvent.MOUSE_UP, onMouseUp);
				mouseDragStartEvent =
					new MouseDragStartEvent(
						MouseDragStartEvent.MOUSE_DRAG_START,
						mouse.stageX,
						mouse.stageY,
						mouse.bubbles,
						mouse.cancelable,
						mouse.localX,
						mouse.localY,
						mouse.relatedObject,
						mouse.ctrlKey,
						mouse.altKey,
						mouse.shiftKey,
						mouse.buttonDown,
						mouse.delta,
						mouse.commandKey,
						mouse.controlKey,
						mouse.clickCount);
			}
			
			private function onMouseMove(mouse:MouseEvent):void {
				cancelDragStart();
				dispatchEvent( mouseDragStartEvent );
			}
			
			private function onMouseUp(mouse:MouseEvent):void {
				cancelDragStart();
			}
			
			private function cancelDragStart():void {
				removeEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
				removeEventListener(MouseEvent.MOUSE_UP, onMouseUp);
			}
			
			private function onDoubleClick():void {
				if (!iconUserUrl) return;
				navigateToURL(new URLRequest(iconUserUrl));
			}
			
			//
			// Mouse Pause
			//
			private function onRollOver(mouse:Event):void {
				mousePauseTimeoutId =
					setTimeout(onMousePause, 1500);
			}
			
			private function onMousePause():void {
				this.statusText.lineNumber = 0; // not select
			}
			
			private function onRollOut(mouse:Event):void {
				clearTimeout( mousePauseTimeoutId );
				if (FaceIcon.selectedFaceIcon != this) {
					this.statusText.lineNumber = 2;
				}
			}
			
			private function isSelected():Boolean {
				return (FaceIcon.selectedFaceIcon
						&& FaceIcon.selectedFaceIcon == this);
			}
			
			private function onClick(mouse:Event):void {
				if (this.isSelected()) {
					unselect();
				} else {
					select();
				}
			}
			
			private function select():void {
				if (FaceIcon.selectedFaceIcon) {
					FaceIcon.selectedFaceIcon.unselect();
				}
				FaceIcon.selectedFaceIcon = this;
				this.statusText.lineNumber = 0;
			}
			
			private function unselect():void {
				FaceIcon.selectedFaceIcon = null;
				this.statusText.lineNumber = 2;
			}
			
		]]>
	</mx:Script>
	<mx:Image id="image" width="48" height="48"/>
	<view:FaceIconText id="nameText" text="Name" lineNumber="1"/>
	<view:FaceIconText id="statusText" text="" lineNumber="2"/>
</mx:VBox>
