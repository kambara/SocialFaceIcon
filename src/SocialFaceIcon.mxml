<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml"
	layout="absolute"
	width="200"
	height="100"
	initialize="onInitialize()"
	closing="onClosing()"
	visible="false"
	applicationActivate="onApplicationActive()"
	applicationDeactivate="onApplicationDeactive()"
	>
	<mx:Script>
		<![CDATA[
			import socialfaceicon.view.FaceIcon;
			import socialfaceicon.model.HumanDockIcon;
			import flash.utils.setTimeout;
			import mx.core.Window;
			import mx.core.Application;
			import org.libspark.thread.EnterFrameThreadExecutor;
			import org.libspark.thread.Thread;
			import socialfaceicon.model.DesktopGroup;
			import socialfaceicon.model.threads.CrawlTwitFriendsThread;
			import socialfaceicon.model.threads.CrawlTwitFriendsTimelineThread;
			import socialfaceicon.model.threads.TwitFriendsTimelineThread;
			import socialfaceicon.model.DesktopIcon;
			import socialfaceicon.model.TwitSession;
			import socialfaceicon.model.Setting;
			import socialfaceicon.model.DB;
			import socialfaceicon.view.DockWindow;
			import socialfaceicon.view.DesktopWindow;
			
			[Embed(source="socialfaceicon/assets/icons/16.png")]
			private var Icon16:Class;
			[Embed(source="socialfaceicon/assets/icons/32.png")]
			private var Icon32:Class;
			[Embed(source="socialfaceicon/assets/icons/48.png")]
			private var Icon48:Class;
			[Embed(source="socialfaceicon/assets/icons/128.png")]
			private var Icon128:Class;
			
			private function onInitialize():void {
				Thread.initialize(new EnterFrameThreadExecutor());
				DB.createTables();
				initTwitter();
				initSystemTrayIcon();
				
				DesktopWindow.instance.hide();
				DesktopIcon.openAll();
				DesktopGroup.openAll();
				HumanDockIcon.openAll();
				
				// Start crawling
				setTimeout(function():void {
					(new CrawlTwitFriendsThread()).start();
				}, 5 * 1000);
				
				setTimeout(function():void {
					(new CrawlTwitFriendsTimelineThread()).start();
				}, 2 * 60 * 1000);
			}
			
			private function initTwitter():void {
				if (Setting.twitterUsername
					&& Setting.twitterUsername)
				{
					TwitSession.start(
							Setting.twitterUsername,
							Setting.twitterPassword);
				}
			}
			
			private function initSystemTrayIcon():void {
				if (!NativeApplication.supportsSystemTrayIcon)
					return;
				
				var menuItemExit:NativeMenuItem = new NativeMenuItem("Exit");
				menuItemExit.addEventListener(
					Event.SELECT,
					function():void {
						Application.application.exit();
					});
				var nativeMenu:NativeMenu = new NativeMenu();
				nativeMenu.addItem(menuItemExit);
				
				var trayIcon:SystemTrayIcon = nativeApplication.icon as SystemTrayIcon
				trayIcon.menu = nativeMenu;
				trayIcon.bitmaps = [new Icon16(),
									new Icon32(),
									new Icon48(),
									new Icon128()];
			}
			
			private function onApplicationActive():void {
				this.frameRate = 60
			}
			
			private function onApplicationDeactive():void {
				FaceIcon.unselect();
				this.frameRate = 1;
			}
			
			private function onClosing():void {
				if (DesktopWindow.instance) {
					DesktopWindow.instance.close();
				}
				DesktopIcon.closeAll();
			}
		]]>
	</mx:Script>
</mx:WindowedApplication>
