<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml"
	layout="absolute"
	width="200"
	height="100"
	initialize="onInitialize()"
	closing="onClosing()"
	close="exit()"
	visible="false"
	applicationActivate="onApplicationActive()"
	applicationDeactivate="onApplicationDeactive()"
	>
	<mx:Script>
		<![CDATA[
			import socialfaceicon.view.friendslist.FriendsListWindow;
			import org.libspark.thread.ThreadState;
			import flash.utils.setInterval;
			import socialfaceicon.model.friendfeed.threads.CrawlFFeedHomeEntriesThread;
			import socialfaceicon.model.friendfeed.threads.UpdateFFeedHomeEntriesThread;
			import socialfaceicon.model.friendfeed.threads.FFeedHomeEntriesThread;
			import socialfaceicon.model.friendfeed.FFeedSession;
			import socialfaceicon.model.friendfeed.threads.FFeedFriendsThread;
			import socialfaceicon.model.facebook.threads.CrawlFBookFriendsThread;
			import socialfaceicon.model.facebook.threads.FBookFriendsThread;
			import socialfaceicon.view.FaceIcon;
			import socialfaceicon.model.HumanDockIcon;
			import flash.utils.setTimeout;
			import mx.core.Window;
			import mx.core.Application;
			import org.libspark.thread.EnterFrameThreadExecutor;
			import org.libspark.thread.Thread;
			import socialfaceicon.model.DesktopGroup;
			import socialfaceicon.model.twitter.threads.CrawlTwitFriendsThread;
			import socialfaceicon.model.twitter.threads.CrawlTwitFriendsTimelineThread;
			import socialfaceicon.model.twitter.threads.TwitFriendsTimelineThread;
			import socialfaceicon.model.DesktopIcon;
			import socialfaceicon.model.twitter.TwitSession;
			import socialfaceicon.model.Setting;
			import socialfaceicon.model.DB;
			import socialfaceicon.view.DockWindow;
			import socialfaceicon.view.DesktopWindow;
			
			[Embed(source="socialfaceicon/assets/icons-embed/16.png")]
			private var Icon16:Class;
			[Embed(source="socialfaceicon/assets/icons-embed/32.png")]
			private var Icon32:Class;
			[Embed(source="socialfaceicon/assets/icons-embed/48.png")]
			private var Icon48:Class;
			[Embed(source="socialfaceicon/assets/icons-embed/128.png")]
			private var Icon128:Class;
			
			private var twitFriends:CrawlTwitFriendsThread;
			private var fbookFriends:CrawlFBookFriendsThread;
			private var ffeedHomeEntries:CrawlFFeedHomeEntriesThread;
			private var twitFriendsTimeline:CrawlTwitFriendsTimelineThread;
			
			private function onInitialize():void {
				Thread.initialize(new EnterFrameThreadExecutor());
				DB.createTables();
				initTwitter();
				initFriendfeed();
				initSystemTrayIcon();
				
				DesktopWindow.instance.hide();
				DesktopIcon.openAll();
				DesktopGroup.openAll();
				HumanDockIcon.openAll();
				
				// Start crawling
				twitFriends = new CrawlTwitFriendsThread();
				setTimeout(function():void {
					twitFriends.start();
				}, 5 * 1000);
				
				fbookFriends = new CrawlFBookFriendsThread();
				//setTimeout(function():void {
					fbookFriends.start();
				//}, 30 * 1000);
				
				ffeedHomeEntries = new CrawlFFeedHomeEntriesThread();
				setTimeout(function():void {
					ffeedHomeEntries.start();
				}, 1 * 60 * 1000);
				
				twitFriendsTimeline = new CrawlTwitFriendsTimelineThread();
				setTimeout(function():void {
					twitFriendsTimeline.start();
				}, 2 * 60 * 1000);

				setInterval(checkThreads, 5 * 60 * 1000);
			}
			
			private function checkThreads():void {
				trace("-- Check threads --");
				if (twitFriends.state == ThreadState.TERMINATED) {
					trace("!!! restore: twitFriends");
					twitFriends = new CrawlTwitFriendsThread();
					twitFriends.start();
				}
				if (fbookFriends.state == ThreadState.TERMINATED) {
					trace("!!! restore: fbookFriends");
					fbookFriends = new CrawlFBookFriendsThread();
					fbookFriends.start();
				}
				if (ffeedHomeEntries.state == ThreadState.TERMINATED) {
					trace("!!! restore: ffeedHomeEntries");
					ffeedHomeEntries = new CrawlFFeedHomeEntriesThread();
					ffeedHomeEntries.start();
				}
				if (twitFriendsTimeline.state == ThreadState.TERMINATED) {
					trace("!!! restore: twitFriendsTimeline");
					twitFriendsTimeline = new CrawlTwitFriendsTimelineThread();
					twitFriendsTimeline.start();
				}
			}
			
			private function initTwitter():void {
				if (Setting.twitterUsername
					&& Setting.twitterUsername)
				{
					TwitSession.start(
							Setting.twitterUsername,
							Setting.twitterPassword);
				}
			}
			
			private function initFriendfeed():void {
				if (Setting.friendfeedUsername
					&& Setting.friendfeedRemoteKey)
				{
					FFeedSession.start(
							Setting.friendfeedUsername,
							Setting.friendfeedRemoteKey);
				}
			}
			
			private function initSystemTrayIcon():void {
				if (!NativeApplication.supportsSystemTrayIcon)
					return;
				
				var menuItemExit:NativeMenuItem = new NativeMenuItem("Exit");
				menuItemExit.addEventListener(
					Event.SELECT,
					function():void {
						Application.application.exit();
					});
				var menuItemFriends:NativeMenuItem = new NativeMenuItem("Friends");
				menuItemFriends.addEventListener(
					Event.SELECT,
					function():void {
						FriendsListWindow.show();
					});
				
				var nativeMenu:NativeMenu = new NativeMenu();
				nativeMenu.addItem(menuItemFriends);
				nativeMenu.addItem(menuItemExit);
				
				var trayIcon:SystemTrayIcon = nativeApplication.icon as SystemTrayIcon
				trayIcon.menu = nativeMenu;
				trayIcon.bitmaps = [new Icon16(),
									new Icon32(),
									new Icon48(),
									new Icon128()];
			}
			
			private function onApplicationActive():void {
				this.frameRate = 60;
				
			}
			
			private function onApplicationDeactive():void {
				FaceIcon.unselect();
				this.frameRate = 1;
			}
			
			private function onClosing():void {
				if (DesktopWindow.instance) {
					DesktopWindow.instance.close();
				}
				DesktopIcon.closeAll();
			}
		]]>
	</mx:Script>
</mx:WindowedApplication>
